buildscript{
    repositories{
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'no.nils:wsdl2java:0.10'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.1.1'
    }
}

plugins {
    id 'groovy'
    id 'java'
    id "no.nils.wsdl2java" version "0.10"
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'net.heithoff'
version '1.0-SNAPSHOT'
description 'WorkdayWSClient is a library that aims to simplify the use of Workday Web Services'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.slf4j:slf4j-api:1.7.25',
            'org.slf4j:slf4j-log4j12:1.7.25',
            'log4j:log4j:1.2.17'
    )

    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile "junit:junit:4.12"
    testCompile "org.spockframework:spock-core:1.2-groovy-2.4"

//    testCompile "org.hamcrest:hamcrest-core:1.3"  // only necessary if Hamcrest matchers are used
//    testRuntime "net.bytebuddy:byte-buddy:1.8.21" // allows mocking of classes (in addition to interfaces)
//    testRuntime "org.objenesis:objenesis:2.6"     // allows mocking of classes without default constructor (together with CGLIB)
}

testSets {
    integrationTest { dirName = 'integration-test' }
}


check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

test {
    useJUnit {
        includeCategories 'net.heithoff.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'net.heithoff.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}



wsdl2javaExt {
    deleteGeneratedSourcesOnClean = true
}

wsdl2java {
    generatedWsdlDir = file("workday-generated-sources")  // target directory for generated source code
    wsdlDir = file("src/main/resources/wsdl") // define to support incremental build
    wsdlsToGenerate = [   //  2d-array of wsdls and cxf-parameters
                          ['src/main/resources/wsdl/Human_Resources.wsdl'],
                          ['src/main/resources/wsdl/Integrations.wsdl'],
                          //['-xjc','-b','bindingfile.xml','src/main/resources/wsdl/secondwsdl.wsdl']
    ]
    locale = Locale.US
}
